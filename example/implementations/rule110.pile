mem 28 + 1 <|

0 while dup 28 < do
  0 while dup 30 < do
    \ Check if the mem at the position is 1
    dup mem + |> if
      mem 100 + 42 <| \ 42 is the ASCII code for '*'
    else
      mem 100 + 32 <| \ 20 is the ASCII code for ' '
    end

    \ write   fd  *buf      count
    SYS_write 1   mem 100 + 1     syscall3

    1 +
  end drop

  mem 100 + 10 <| \ 10 is the ASCII code for '\n'
  SYS_write 1 mem 100 + 1 syscall3

  \ board[0] << 1
  mem 0 + |> 1 << 
  \ ^ | board[1]
  mem 1 + |> |

  1 while dup 29 < do
                       \ pattern index

    swap               \ index pattern
    1 << 7 &           \ index pattern
    over               \ index pattern index
    mem + 1 + |>       \ index pattern board[index+1]
    |                  \ index new_pattern
    dup2               \ index new_pattern index new_pattern
    110                \ index new_pattern index new_pattern 110
    swap >> 1 &        \ index new_pattern index final_pattern
    swap mem + swap <| \ index new_pattern

    swap

    1 +
  end drop drop

  1 +
end drop

\ Exit
SYS_exit 69 syscall1
